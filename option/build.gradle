apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 11
        versionName "1.1"

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
//-------------打jar包开始---------------
task makeJar(type: Copy) {
    delete 'build/libs/imageselect-1.0.jar'
    from('build/intermediates/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', 'imageselect-1.0.jar')
}

makeJar.dependsOn(build)
//运行命令gradlew makeJar  导出jar包
//-------------打jar包结束---------------
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
}
//--------编码gbk的不可映射字符------------

//----------------------------------发布--------------------------------------------
// 这个version是区分library版本的，因此当我们需要更新library时记得修改这个version
version = "1.0.3"
def siteUrl = 'https://github.com/452693688/SelectImage'      // 项目的主页
def gitUrl = 'https://github.com/452693688/SelectImage.git'   // Git仓库的url
// Maven Group ID for the artifact，一般填你唯一的包名
group = "com.guomin.image.option"
install {

    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name '图片选择器'  //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'guomin'       //填写开发者基本信息
                        name 'guomin'
                        email '452693688@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
     classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "utile"  //发布到Bintray名为“utile”的仓库里
        name = "SelectImage"  //发布到Bintray上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }

   // tasks.withType(JavaCompile) {
    //    options.encoding = "UTF-8"
   // }
}
//--------------1.运行gradlew install 2.运行gradlew bintrayUpload--------------
